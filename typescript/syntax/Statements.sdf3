module Statements

imports
  Common
  Expressions
  Modules
  Classes
  Functions
  Types

template options

  tokenize: "({;"
 
context-free priorities
  Statement.ValueReturn > Statement.EmptyReturn

context-free syntax
  Statement.EmptyReturn = <return;>
  Statement.ValueReturn = <return <Expression>>
  Statement.IfStatement = <if (<Expression>) <Statement>>
  Statement.IfElseStatement = <
    if (<Expression>) <Statement>
    else <Statement>
  >
  Statement.BlockStatement = <{
    <StatementList>
  }>
  Statement.ExpressionStatement = Expression
  
  Statement = VarDeclaration
  Statement = InterfaceDeclaration
  Statement = ExportDeclaration
  Statement = ImportDeclaration
  VarDeclaration.VariableDeclaration = <<VariableDeclarationType> <{VariableDeclaration ","}+>>
  VariableDeclarationType.Let = <let>
  VariableDeclarationType.Const = <const>
  VariableDeclarationType.Var = <var>
  VariableDeclaration.VariableDeclarationNoInitializer = <<NameWithOptionalType>>
  VariableDeclaration.VariableDeclarationWithInitializer = <<NameWithOptionalType> = <Expression>>
  
//  Statement.EmptyInterfaceDeclaration = <interface <ID>>
  Statement.InterfaceDeclarationWithTypeParameters = <interface <ID><TypeParameters><StructureType>>
  InterfaceDeclaration.InterfaceDeclaration = <interface <ID> { <{NameWithType ","}*> }> 

  StatementList.StatementList = <<{Statement ";\n"}*>>
  